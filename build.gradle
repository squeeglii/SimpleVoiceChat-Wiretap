plugins {
    id 'java'
    id 'fabric-loom' version '1.7-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.modrinth.minotaur' version '2.+'
    id 'mod-update' version '2.0.0'
}

sourceCompatibility = JavaVersion.toVersion(java_version)
targetCompatibility = JavaVersion.toVersion(java_version)

apply from: "$rootDir/publishing.gradle"

String minimum_minecraft_version = getMinimumSupportedVersion(project.properties)

String mod_version = "%s+%s.fabric".formatted(
        mod_base_version,
        minimum_minecraft_version
)

archivesBaseName = archives_base_name
version = mod_version
group = maven_group

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://maven.fabricmc.net/' }
    maven { url = 'https://maven.maxhenkel.de/repository/public' }
    maven {
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    runs {
        client {
            client()
            runDir = "run/client"
            ideConfigGenerated = true
        }

        server {
            server()
            runDir = "run/server"
            ideConfigGenerated = true
        }
    }
}

runClient.doFirst {
    args = ['--username', "SVC-Dev.${new Random().nextInt(1000)}"]
}

dependencies {
    minecraft "com.mojang:minecraft:${minimum_minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-command-api-v2',
            'fabric-networking-api-v1',
            'fabric-lifecycle-events-v1'
    ]

    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, fabric_version)))
    }

    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"
    shadow "de.maxhenkel.configbuilder:configbuilder:${configbuilder_version}"

    implementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"

    modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_mod_version}"
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
}

processResources {
    filteringCharset "UTF-8"

    HashMap modifiedSrc = new HashMap<>(project.properties)
    modifiedSrc.put("mod_version", mod_version)
    modifiedSrc.put("minimum_minecraft_version", minimum_minecraft_version)

    modifiedSrc.put("tested_fabric_versions", getFabricVersionString(modifiedSrc))
    modifiedSrc.put("tested_forge_versions", getForgeVersionString(modifiedSrc))
    modifiedSrc.put("tested_neoforge_versions", getForgeVersionString(modifiedSrc))

    filesMatching("fabric.mod.json") {
        expand modifiedSrc
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release = Integer.valueOf(java_version)
}

java {
    withSourcesJar()
}

jar {
    from('LICENSE') {
        rename { "${it}_${archivesBaseName}" }
    }
}

curseforge {
    String[] testedVersions = getSupportedVersions(project.properties);

    if(testedVersions.length == 0)
        throw new IllegalStateException("At least one version must be defined under 'tested_minecraft_versions' for publishing")

    apiKey = getCurseToken(project)

    project {
        id = curse_id
        changelogType = 'markdown'
        changelog = file('changelog.md')
        releaseType = release_type


        for(String ver: testedVersions) {
            addGameVersion ver
        }

        addGameVersion 'Fabric'
        addGameVersion 'Java %s'.formatted(java_version)

        mainArtifact(file("${buildDir}/libs/${archivesBaseName}-${version}.jar")) {
            displayName = "[FABRIC][${minimum_minecraft_version}] ${mod_name} ${mod_base_version}"
            relations {
                requiredDependency 'simple-voice-chat'
            }
        }

        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
    }

    options {
        var shouldPublish = project.properties["debug_publishing"]
        if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
            debug = true

        forgeGradleIntegration = false
    }
}


tasks.modrinth.configure({
    group = 'upload'
})
tasks.modrinthSyncBody.configure({
    group = 'upload'
})

modrinth {
    String[] testedVersions = getSupportedVersions(project.properties)

    if(testedVersions.length == 0)
        throw new IllegalStateException("At least one version must be defined under 'tested_minecraft_versions' for publishing")

    token = getModrinthToken(project)
    projectId = modrinth_id
    versionType = release_type.toUpperCase()
    changelog = file('changelog.md').text

    versionName = "[FABRIC][${minimum_minecraft_version}] ${mod_name} ${mod_base_version}"
    versionNumber = "${mod_version}"

    uploadFile = remapJar
    loaders = ['fabric', 'quilt']
    gameVersions = Arrays.asList(testedVersions)

    syncBodyFrom = file('readme.md').text

    dependencies {
        required.project '9eGKb6K1' // Simple Voice Chat
    }

    var shouldPublish = project.properties["debug_publishing"]
    if(shouldPublish != null && shouldPublish.toString().equalsIgnoreCase("true"))
        setDebugMode(true)
}
tasks.modrinth.dependsOn(build)

modUpdate {
    serverURL = 'https://update.maxhenkel.de/'
    modID = mod_id
    gameVersion = minimum_minecraft_version
    modLoader = 'fabric'
    modVersion = mod_version
    changelogFile = file('changelog.md')
    releaseType = release_type
    tags = recommended == 'true' ? ['recommended'] : []
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveClassifier = 'shadow-dev'
    relocate 'de.maxhenkel.configbuilder', "de.maxhenkel.${mod_id}.configbuilder"
}

remapJar {
    dependsOn shadowJar
    inputFile = shadowJar.archiveFile.get()
}